#!/usr/bin/env fish
# Interactive theme selector using rofi dmenu
# Usage: hawker-rofi-theme-select

set themes_dir "$HOME/.local/share/hawker/themes"

if not test -d "$themes_dir"
    notify-send "Theme Selector" "Themes directory not found" -t 3000
    exit 1
end

# Get list of available themes
set theme_list (hawker-theme-list)

# Get current theme for highlighting
set current_theme (hawker-theme-current 2>/dev/null | string lower | string replace -a ' ' '-')

# Build display list with current marker and track index
set display_list
set current_index -1
set index 0
for theme in $theme_list
    if test "$theme" = "$current_theme"
        set display_list $display_list "● $theme"
        set current_index $index
    else
        set display_list $display_list "  $theme"
    end
    set index (math "$index + 1")
end

# Show rofi picker, pre-select current theme
set rofi_args -dmenu -i -p "Select Theme" -no-custom -theme-str 'window {width: 400px; height: 500px;}'
if test $current_index -ge 0
    set rofi_args $rofi_args -selected-row $current_index
end
set selected (printf '%s\n' $display_list | rofi $rofi_args)

if test -z "$selected"
    exit 0
end

# Clean selection and convert to theme name
set theme_name (echo $selected | sed 's/^[● ] *//; s/ /-/g' | string lower)
set pretty_name (echo $theme_name | sed 's/-/ /g; s/\b\(.\)/\u\1/g')

# Apply in background
nohup hawker-theme-set "$theme_name" >/dev/null 2>&1 &
